/*
 * Copyright (C) 2014 Andrew Comminos
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
    google()
}

dependencies {
    implementation project(":libraries:Jumble")
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:palette-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'info.guardianproject.netcipher:netcipher:2.1.0'
}


// Keep my credentials private :^)
def signingFile = file 'signing.gradle'
if (signingFile.exists()) apply from: 'signing.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        // TODO 21?
        minSdkVersion 14
        targetSdkVersion 28
        applicationId "com.morlunk.mumbleclient"
        versionCode 73
        versionName "3.3.0-rc1"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        buildConfigField "boolean", "DONATE_NAG", "false"
    }

    flavorDimensions "version"
    productFlavors {
        official {
            dimension "version"
            applicationId "com.morlunk.mumbleclient"
        }
        free {
            dimension "version"
            applicationId "com.morlunk.mumbleclient.free"
            buildConfigField "boolean", "DONATE_NAG", "true"
        }
        jenkins {
            dimension "version"
            // Abuse Jenkins environment variables. Neat.
            def env = System.getenv()
            if (env.containsKey("BUILD_NUMBER") && env.containsKey("BUILD_DISPLAY_NAME")) {
                versionCode Integer.parseInt(env.get("BUILD_NUMBER"))
                versionName env.get("BUILD_DISPLAY_NAME")
            } else {
                versionCode 1
                versionName "Unknown Nightly"
            }
            applicationId "com.morlunk.mumbleclient.jenkins"
        }
    }

    lintOptions {
        abortOnError false
    }
}
